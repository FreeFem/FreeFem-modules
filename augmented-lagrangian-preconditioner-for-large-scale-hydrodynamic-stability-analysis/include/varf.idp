/*
   Author(s): Johann Moulin <johann.moulin@onera.fr>
              Pierre Jolivet <pierre.jolivet@enseeiht.fr>
              Olivier Marquet <olivier.marquet@onera.fr>
        Date: 2018-07-20

   Copyright (C) 2018-     Office national d'études et de recherches aérospatiales
                 2018-     Centre National de la Recherche Scientifique

   This script is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

   If you use this script, you are kindly asked to cite the following article:

   "Augmented Lagrangian Preconditioning for Large-Scale Hydrodynamic Stability",
   J. Moulin, P. Jolivet, and O. Marquet (2019).
 */

IFMACRO(!original)
IFMACRO(!opt)
macro opt()true//
ENDIFMACRO
ENDIFMACRO
macro grad(u)[dx(u), dy(u), dz(u)]//
macro div(u)(dx(u#1) + dy(u#2) + dz(u#3))//
macro UgradV(u, v)[[u#1, u#2, u#3]' * [dx(v#1), dy(v#1), dz(v#1)],
                   [u#1, u#2, u#3]' * [dx(v#2), dy(v#2), dz(v#2)],
                   [u#1, u#2, u#3]' * [dx(v#3), dy(v#3), dz(v#3)]]//
macro UgradVW(u, v, w)(u#1 * (dx(v#1) * w#1 + dx(v#2) * w#2 + dx(v#3) * w#3)
                     + u#2 * (dy(v#1) * w#1 + dy(v#2) * w#2 + dy(v#3) * w#3)
                     + u#3 * (dz(v#1) * w#1 + dz(v#2) * w#2 + dz(v#3) * w#3))//

varf vJ([u1, u2, u3, p], [v1, v2, v3, q]) = int3d(th)(
IFMACRO(opt)
      UgradVW(uc, u, v) + UgradVW(u, uc, v)
ENDIFMACRO
IFMACRO(original)
      (UgradV(uc, u) + UgradV(u, uc))' * [v1, v2, v3]
ENDIFMACRO
    )
    +
 int3d(th, qforder = 3)(
      nu * (grad(u1)' * grad(v1) +
            grad(u2)' * grad(v2) +
            grad(u3)' * grad(v3))
    + (gamma * div(u) - p) * div(v)
    - div(u) * q
    )
    // Dirichlet (inlet + plate)
    + on(1, 2, u1 = 0, u2 = 0, u3 = 0)
    // far-field (zmax and ymax)
    + on(6, 7, u1 = 0);
